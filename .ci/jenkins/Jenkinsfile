@Library('jenkins-pipeline-shared-libraries')_

pipeline {
    agent {
        docker { 
            image env.AGENT_DOCKER_BUILDER_IMAGE
            args env.AGENT_DOCKER_BUILDER_ARGS
        }
    }
    options {
        timeout(time: 120, unit: 'MINUTES')
    }
    stages {
        stage('Initialization') {
            steps {
                script {
                    sh 'printenv'
                    
                    dir(getRepoName()) {
                        // githubscm.checkoutIfExists(getRepoName(), getChangeAuthor(), getChangeBranch(), 'apache', getChangeTarget(), true, credentials = [token: getGitAuthorCredentialsId(), usernamePassword: 'kie-ci'])
                        // TODO Test pupose
                        deleteDir()
                        checkout(githubscm.resolveRepository('kogito-images', 'radtriste', 'fix-pipelines', false))
                    }
                }
            }
        }
        stage('Validate CeKit Image and Modules descriptors') {
            steps {
                script {
                    dir(getRepoName()) {
                        sh '''
                            curl -Ls https://github.com/kiegroup/kie-cloud-tools/releases/download/v1.3.6/cekit-image-validator-runner.tgz --output cekit-image-validator-runner.tgz
                            tar -xzvf cekit-image-validator-runner.tgz
                            chmod +x cekit-image-validator-runner
                        '''
                        sh './cekit-image-validator-runner modules/'
                        getImages().each { image -> sh "./cekit-image-validator-runner ${image}-image.yaml" }
                    }
                }
            }
        }
        stage('Build & Test Images') {
            steps {
                script {
                    dir(getRepoName()) {
                        parallelStages = [:]
                        for(String image : getImages()){
                            parallelStages[image] = createBuildAndTestStageClosure(image)
                        }
                        parallel parallelStages
                    }
                }
            }
        }
    }
    post {
        unsuccessful {
            script {
                pullrequest.postComment(util.getMarkdownTestSummary('PR', "${BUILD_URL}", 'GITHUB'))
            }
        }
    }
}

Closure createBuildAndTestStageClosure(String image) {
    return {
        stage("Build&Test ${image}") {
            List buildParams = []
            buildParams.add(string(name: "DISPLAY_NAME", value: "PR #${ghprbPullId} - ${image}: ${ghprbPullLink}"))
            buildParams.add(string(name: 'BUILD_IMAGE_NAME', value: image))
            buildParams.add(string(name: 'SOURCE_AUTHOR', value: getChangeAuthor()))
            buildParams.add(string(name: 'SOURCE_BRANCH', value: getChangeBranch()))
            buildParams.add(string(name: 'TARGET_BRANCH', value: getChangeTarget()))
            buildParams.add(string(name: 'BUILD_KOGITO_APPS_REF', value: getChangeTarget()))
            
            githubscm.prepareCommitStatusInformationForPullRequest(getRepoName(), getChangeAuthor(), getChangeBranch(), 'apache', getGitAuthorCredentialsId())
            githubscm.updateGithubCommitStatus(image, 'PENDING', 'Queued', getRepoName())
            
            // Keep executing so we can cancel all if needed
            def job = build(job: "${getRepoName()}.build-image", wait: true, parameters: buildParams, propagate: false)
            if (job.result != 'SUCCESS') {
                if (job.result == 'UNSTABLE') {
                    unstable("Tests on ${image} seems to have failed")
                } else {
                    error("Error building ${image}. Please check the logs of the job: ${job.absoluteUrl}")
                }
            }
        }
    }
}

String[] getImages() {
    if (env.IMAGES_LIST) {
        return env.IMAGES_LIST.split(',')
    }
    return sh(returnStdout: true, script: "make list | tr '\\n' ','").trim().split(',')
}

String getChangeAuthor() {
    return CHANGE_AUTHOR
}

String getChangeBranch() {
    return CHANGE_BRANCH
}

String getChangeTarget() {
    return CHANGE_TARGET
}

String getRepoName() {
    return env.REPO_NAME
}

String getGitAuthorCredentialsId() {
    return env.AUTHOR_CREDS_ID
}
