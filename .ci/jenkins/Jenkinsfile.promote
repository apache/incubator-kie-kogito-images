// Promote images
@Library('jenkins-pipeline-shared-libraries')_

deployProperties = [:]

pipeline {
    agent {
        label 'rhel8 && podman && !built-in'
    }

    options {
        timeout(time: 120, unit: 'MINUTES')
    }
    // parameters {
    // For parameters, check into ./dsl/jobs.groovy file
    // }

    environment {
        // Static env is defined into ./dsl/jobs.groovy file

        KOGITO_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")

        // Linked to node label
        CONTAINER_ENGINE = 'podman'
        CONTAINER_ENGINE_TLS_OPTIONS = '--tls-verify=false'

        PR_BRANCH_HASH = "${util.generateHash(10)}"
    }

    stages {
        stage('Initialization') {
            steps {
                script {
                    clean()

                    if (params.DISPLAY_NAME) {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    readDeployProperties()

                    assert getProjectVersion()
                    assert getBuildBranch() == util.getReleaseBranchFromVersion(getProjectVersion())

                    // Login old registry
                    if (getOldImageRegistryCredentials() != '') {
                        getContainerEngineService().loginContainerRegistry(getOldImageRegistry(), getOldImageRegistryCredentials())
                    }

                    // Login new registry
                    if (getNewImageRegistryCredentials() != '') {
                        getContainerEngineService().loginContainerRegistry(getNewImageRegistry(), getNewImageRegistryCredentials())
                    }

                    dir(getRepoName()) {
                        checkoutRepo()
                    }

                    cloud.installSkopeo()
                }
            }
        }

        stage('Promote images') {
            steps {
                script {
                    dir(getRepoName()) {
                        promoteImages()
                    }
                }
            }
        }

        stage('Make Quay images public') {
            when {
                expression { isQuayRegistry(getNewImageRegistry()) }
            }
            steps {
                script {
                    dir(getRepoName()) {
                        makeQuayNewImagesPublic()
                    }
                }
            }
        }

        stage('Merge PR and tag') {
            steps {
                script {
                    dir(getRepoName()) {
                        // Merge PR
                        String prLink = getDeployProperty("${getRepoName()}.pr.link")
                        if (prLink) {
                            githubscm.mergePR(prLink, getGitAuthorCredsID())
                            githubscm.pushObject('origin', getBuildBranch(), getGitAuthorCredsID())
                        }
                    }
                }
            }
        }

        stage('Create release') {
            steps {
                script {
                    if (githubscm.isReleaseExist(getGitTag(), getGitAuthorCredsID())) {
                        githubscm.deleteReleaseAndTag(getGitTag(), getGitAuthorCredsID())
                    }
                    githubscm.createReleaseWithGeneratedReleaseNotes(getGitTag(), getBuildBranch(), githubscm.getPreviousTag(getGitTag()), getGitAuthorCredsID())
                    githubscm.updateReleaseBody(getGitTag(), getGitAuthorCredsID())
                }
            }
        }
    }
    post {
        unsuccessful {
            sendUnsuccessfulNotification()
        }
        cleanup {
            clean()
        }
    }
}

ContainerEngineService getContainerEngineService() {
    return new ContainerEngineService(this)
}

void sendUnsuccessfulNotification() {
    if (params.SEND_NOTIFICATION) {
        mailer.sendMarkdownTestSummaryNotification('Promote', getNotificationSubject(), [env.KOGITO_CI_EMAIL_TO])
    } else {
        echo 'No notification sent per configuration'
    }
}

void sendNotification(String body) {
    emailext body: body,
        subject: getNotificationSubject()
    to: env.KOGITO_CI_EMAIL_TO
}

String getNotificationSubject() {
    return "[${getBuildBranch()}] Kogito Images"
}

void checkoutRepo() {
    deleteDir()
    checkout(githubscm.resolveRepository(getRepoName(), getGitAuthor(), getBuildBranch(), false))
    // need to manually checkout branch since on a detached branch after checkout command
    sh "git checkout ${getBuildBranch()}"
}

void clean() {
    cleanWs()
    util.cleanNode()
    cloud.cleanSkopeo()
}

void promoteImages() {
    for (String imageName : getImages()) {
        promoteImage(getOldImageFullTag(imageName), getNewImageFullTag(imageName, getNewImageTag()))
        if (isDeployLatestTag()) {
            promoteImage(getOldImageFullTag(imageName), getNewImageFullTag(imageName, 'latest'))
        }
        String reducedTag = getReducedTag()
        if (reducedTag) {
            promoteImage(getOldImageFullTag(imageName), getNewImageFullTag(imageName, reducedTag))
        }
    }
}

void promoteImage(String oldImageName, String newImageName) {
    cloud.skopeoCopyRegistryImages(oldImageName, newImageName, Integer.parseInt(env.MAX_REGISTRY_RETRIES))
}

// Set images public on quay. Useful when new images are introduced.
void makeQuayNewImagesPublic() {
    String namespace = getNewImageNamespace()
    for (String imageName : getImages()) {
        String repository = getFinalImageName(imageName, getNewImageNameSuffix())
        echo "Check and set public if needed Quay repository ${namespace}/${repository}"
        try {
            cloud.makeQuayImagePublic(namespace, repository, [ usernamePassword: getNewImageRegistryCredentials()])
        } catch (err) {
            echo "[ERROR] Cannot set image quay.io/${namespace}/${repository} as visible"
        }
    }
}

String getOldImageFullTag(String imageName) {
    return "${getOldImageRegistry()}/${getOldImageNamespace()}/${getFinalImageName(imageName, getOldImageNameSuffix())}:${getOldImageTag()}"
}

String getNewImageFullTag(String imageName, String tag) {
    return "${getNewImageRegistry()}/${getNewImageNamespace()}/${getFinalImageName(imageName, getNewImageNameSuffix())}:${tag}"
}

String getFinalImageName(String imageName, String suffix) {
    return suffix ? "${imageName}-${suffix}" : imageName
}

boolean isDeployLatestTag() {
    return params.DEPLOY_WITH_LATEST_TAG
}

String getReducedTag() {
    try {
        return cloud.getReducedTag(getNewImageTag())
    } catch (error) {
        echo "${getNewImageTag()} cannot be reduced to the format X.Y"
    }
    return ''
}

void readDeployProperties() {
    String deployUrl = params.DEPLOY_BUILD_URL
    if (deployUrl != '') {
        if (!deployUrl.endsWith('/')) {
            deployUrl += '/'
        }
        sh "wget ${deployUrl}artifact/deployment.properties"
        deployProperties = readProperties file: 'deployment.properties'
        echo deployProperties.collect { entry -> "${entry.key}=${entry.value}" }.join('\n')
    }
}
boolean hasDeployProperty(String key) {
    return deployProperties[key] != null
}
String getDeployProperty(String key) {
    if (hasDeployProperty(key)) {
        return deployProperties[key]
    }
    return ''
}
String getParamOrDeployProperty(String paramKey, String deployPropertyKey) {
    if (params[paramKey] != '') {
        return params[paramKey]
    }
    return getDeployProperty(deployPropertyKey)
}

////////////////////////////////////////////////////////////////////////
// Utils
////////////////////////////////////////////////////////////////////////

String getRepoName() {
    return env.REPO_NAME
}

String getProjectVersion() {
    return getParamOrDeployProperty('PROJECT_VERSION' , 'project.version')
}

String getKogitoArtifactsVersion() {
    artifactsVersion = getParamOrDeployProperty('KOGITO_ARTIFACTS_VERSION' , 'kogito_artifacts.version')
    return artifactsVersion ?: getProjectVersion()
}

String getGitTag() {
    return params.GIT_TAG != '' ? params.GIT_TAG : getProjectVersion()
}

String getBuildBranch() {
    return params.BUILD_BRANCH_NAME
}

String getGitAuthor() {
    return env.GIT_AUTHOR
}

String getGitAuthorCredsID() {
    return env.AUTHOR_CREDS_ID
}

boolean isQuayRegistry(String registry) {
    return registry == 'quay.io'
}

////////////////////////////////////////////////////////////////////////
// Old image information
////////////////////////////////////////////////////////////////////////

String getOldImagePrefix() {
    return "${getRepoName()}.image"
}

String getOldImageRegistryCredentials() {
    return params.BASE_IMAGE_REGISTRY_CREDENTIALS
}

String getOldImageRegistry() {
    return getParamOrDeployProperty('BASE_IMAGE_REGISTRY' , "${getOldImagePrefix()}.registry")
}

String getOldImageNamespace() {
    return getParamOrDeployProperty('BASE_IMAGE_NAMESPACE' , "${getOldImagePrefix()}.namespace")
}

String getOldImageNames() {
    return getParamOrDeployProperty('BASE_IMAGE_NAMES' , "${getOldImagePrefix()}.names")
}

String getOldImageNameSuffix() {
    return getParamOrDeployProperty('BASE_IMAGE_NAME_SUFFIX' , "${getOldImagePrefix()}.name-suffix")
}

String getOldImageTag() {
    return getParamOrDeployProperty('BASE_IMAGE_TAG' , "${getOldImagePrefix()}.tag")
}

////////////////////////////////////////////////////////////////////////
// New image information
////////////////////////////////////////////////////////////////////////

String getNewImageRegistryCredentials() {
    return params.PROMOTE_IMAGE_REGISTRY_CREDENTIALS
}

String getNewImageRegistry() {
    return params.PROMOTE_IMAGE_REGISTRY
}

String getNewImageNamespace() {
    return params.PROMOTE_IMAGE_NAMESPACE
}

String getNewImageNameSuffix() {
    return params.PROMOTE_IMAGE_NAME_SUFFIX
}

String getNewImageTag() {
    return params.PROMOTE_IMAGE_TAG
}

String[] getImages() {
    String images = getOldImageNames() ?: env.IMAGES_LIST ?: runPythonCommand("make list | tr '\\n' ','", true).trim()
    return images.split(',')
}

void runPythonCommand(String cmd, boolean stdout = false) {
    return util.runWithPythonVirtualEnv(cmd, 'cekit', stdout)
}
